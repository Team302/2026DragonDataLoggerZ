plugins {
    id "java"
    id 'application'
    id 'com.gradleup.shadow' version '8.3.5'
    id "edu.wpi.first.GradleRIO" version "2026.2.1"
    id 'edu.wpi.first.WpilibTools' version '2.1.0'
}

application {
    mainClass = 'pi.logger.PiLogger'
}

repositories {
    mavenCentral()
    maven { url = 'https://frcmaven.wpi.edu/artifactory/release/' }
}

wpilibTools.deps.wpilibVersion = wpi.versions.wpilibVersion.get()

def nativeConfigName = 'wpilibNatives'
def nativeConfig = configurations.create(nativeConfigName)

def nativeTasks = wpilibTools.createExtractionTasks {
    configurationName = nativeConfigName
}

nativeTasks.addToSourceSetResources(sourceSets.main)
nativeConfig.dependencies.add wpilibTools.deps.wpilib("wpimath")
nativeConfig.dependencies.add wpilibTools.deps.wpilib("wpinet")
nativeConfig.dependencies.add wpilibTools.deps.wpilib("wpiutil")
nativeConfig.dependencies.add wpilibTools.deps.wpilib("ntcore")
nativeConfig.dependencies.add wpilibTools.deps.wpilib("cscore")




dependencies {
    //implementation wpi.java.deps.wpilib()
    //implementation wpi.java.vendor.java()

    implementation wpilibTools.deps.wpilibJava("wpiutil")
    implementation wpilibTools.deps.wpilibJava("wpimath")
    implementation wpilibTools.deps.wpilibJava("wpinet")
    implementation wpilibTools.deps.wpilibJava("ntcore")
    implementation wpilibTools.deps.wpilibJava("cscore")
    implementation wpilibTools.deps.wpilibJava("cameraserver")

    // Add JNI libraries for runtime
    runtimeOnly wpilibTools.deps.wpilib("wpimath")
    runtimeOnly wpilibTools.deps.wpilib("wpinet")
    runtimeOnly wpilibTools.deps.wpilib("wpiutil")
    runtimeOnly wpilibTools.deps.wpilib("ntcore")
    runtimeOnly wpilibTools.deps.wpilib("cscore")

    implementation group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: wpi.versions.jacksonVersion.get()
    implementation group: "com.fasterxml.jackson.core", name: "jackson-core", version: wpi.versions.jacksonVersion.get()
    implementation group: "com.fasterxml.jackson.core", name: "jackson-databind", version: wpi.versions.jacksonVersion.get()

    implementation group: "org.ejml", name: "ejml-simple", version: wpi.versions.ejmlVersion.get()
    implementation group: "us.hebi.quickbuf", name: "quickbuf-runtime", version: wpi.versions.quickbufVersion.get();
}

jar {
    manifest {
        attributes 'Main-Class': 'pi.logger.PiLogger'
    }
}

shadowJar {
    archiveBaseName = "PiLogger"
    archiveVersion = ""
    exclude("module-info.class")
    archiveClassifier.set(wpilibTools.currentPlatform.platformName)
    manifest {
        attributes 'Main-Class': 'pi.logger.PiLogger'
    }
    // Merge service files for proper loading
    mergeServiceFiles()
}

// Make shadowJar include the extracted natives as resources
// nativeTasks is already added to sourceSets.main.resources, so no explicit dependency needed

assemble.dependsOn shadowJar

configurations {
    arm64Natives {
        // Make this configuration use the same repositories as regular dependencies
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    arm64Natives wpi.java.deps.wpilibJniRelease(wpi.platforms.linuxarm64)
    arm64Natives wpi.java.vendor.jniRelease(wpi.platforms.linuxarm64)
}

wrapper {
    gradleVersion = '8.11'
}

task shadowJarArm64(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveBaseName = "PiLogger"
    archiveVersion = ""
    archiveClassifier.set("linuxarm64-cross")
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
    exclude("module-info.class")
    manifest {
        attributes 'Main-Class': 'pi.logger.PiLogger'
    }
    mergeServiceFiles()
    
    // Extract ARM64 natives from ZIP files and include them
    from(project.configurations.arm64Natives.collect { 
        it.name.endsWith('.zip') ? zipTree(it) : it 
    })
}
